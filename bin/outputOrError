#!/usr/bin/awk -f

BEGIN {
    errorStatus = 1
    ARGB = 1
    while (1) {
	if (ARGV[ARGB] ~ /^(--(help|man|usage)|-[Hh?])$/) {
	    print "If output is piped into the command, it is passed through as-is."
	    print "If there's no output, exit with 1 / N."
	    print ""
	    print "Usage: outputOrError [-w|--non-whitespace] [--status N] FILE [...] [-?|-H|--man|--usage]"
	    print "Usage: cat FILE [...] | outputOrError [-w|--non-whitespace] [--status N]"
	    print ""
	    print "    --non-whitespace|-w	Fail if output just consists of whitespace."
	    print "    --status N		Fail with N instead of 1."
	    print ""
	    print "SEE ALSO:"
	    print "- outputOr is a generalization that can directly execute shell commands if no"
	    print "  output."
	    output = 1  # Make the END block succeed.
	    exit
	} else if (ARGV[ARGB] ~ /^--non-whitespace|-w$/) {
	    delete ARGV[ARGB++]
	    isNonWhitespace = 1
	} else if (ARGV[ARGB] == "--status") {
	    delete ARGV[ARGB++]
	    errorStatus = ARGV[ARGB]
	    delete ARGV[ARGB++]
	} else {
	    break
	}
    }
}

{ printf("%s%s", $0, RT); }

! isNonWhitespace { if ($0 != "") output = 1; }
! isNonWhitespace && 2 { output = 1; }

isNonWhitespace && /\S/ { output = 1; }

END { if (! output) exit errorStatus; }
